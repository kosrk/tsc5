#include "imports/stdlib.fc";

forall X -> tuple ut(X x) asm "NOP";
(int) tlen (tuple t) asm "TLEN";
forall X -> int is_null (X x) asm "ISNULL";
forall X -> (tuple, ()) push_back (tuple tail, X head) asm "CONS";
forall X -> (tuple, (X)) pop_back (tuple t) asm "UNCONS";
forall X -> (tuple, ()) tset(tuple t, int idx, X x) asm(t x idx) "SETINDEXVAR";
forall X -> (tuple, X) ~tpop(tuple t) asm "TPOP";

const int x = 88; ;; X
const int d = 46; ;; .
const int q = 63; ;; ?
const int s = 83; ;; S
const int e = 69; ;; E
const int p = 33; ;; !

const int step_w = 1;
const int prob_w = 1000;
const int wall_w = 1000000;
const int max_w = 1000000000;

global int g_n;
global int g_m;
global tuple g_maze;
global tuple lmap;

() recv_internal(int my_balance, int msg_value, cell in_msg_full, slice in_msg_body) impure {
    return();
}

tuple write_at(tuple in_tup, int in_i, int in_j, int val) inline {
    var row = in_tup.at(in_i);
    row~tset(in_j, val);
    in_tup~tset(in_i, row);
    return in_tup;
}

(int, int, int) get_path(int old_i, int old_j, int i, int j, int old_w) inline {
    if (i >= 0) & (i < g_n) & (j >= 0) & (j < g_m) {
        var new_w = lmap.at(i).at(j);
        if (new_w < old_w) & (new_w != -1)  {
            return (i, j, new_w);
        }
    }
    return (old_i, old_j, old_w);
}

(tuple, tuple, tuple, int) check_for(tuple d_queue, tuple p_queue, tuple w_queue, int i, int j, int w, int end) inline {
    if (i >= 0) & (i < g_n) & (j >= 0) & (j < g_m) {
        var symb = g_maze.at(i).at(j);
        if symb == e {
            return (d_queue, p_queue, w_queue, -1);
        }
        if symb == d {
            if lmap.at(i).at(j) == -1 {
                d_queue~push_back(ut([i, j, w])); ;; dot cells must be extracted first
                lmap = lmap.write_at(i, j, w);
            }
            return (d_queue, p_queue, w_queue, end);
        }
        if symb == q {
            var new_w = w + prob_w;
            if lmap.at(i).at(j) == -1 {
                p_queue~push_back(ut([i, j, new_w])); ;; dot cells must be extracted first
                lmap = lmap.write_at(i, j, new_w);
            }
            return (d_queue, p_queue, w_queue, end);
        }
        if symb == x {
            var new_w = w + wall_w;
            if lmap.at(i).at(j) == -1 {
                w_queue~push_back(ut([i, j, new_w])); ;; dot cells must be extracted first
                lmap = lmap.write_at(i, j, new_w);
            }
            return (d_queue, p_queue, w_queue, end);
        }
    }
    return (d_queue, p_queue, w_queue, end);
}

(tuple, int) step_forward(tuple queue, tuple batch) inline {

    var d_queue = null();
    var p_queue = null();
    var w_queue = null();
    var end = 0;

    while (~ batch.is_null()) {
        var cur = batch~pop_back();
        var cur_i = cur.at(0);
        var cur_j = cur.at(1);
        var w = lmap.at(cur_i).at(cur_j) + step_w;
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i, cur_j - 1, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i + 1, cur_j, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i, cur_j + 1, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i - 1, cur_j, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i + 1, cur_j + 1, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i + 1, cur_j - 1, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i - 1, cur_j + 1, w, end);
        (d_queue, p_queue, w_queue, end) = check_for(d_queue, p_queue, w_queue, cur_i - 1, cur_j - 1, w, end);
    }

    tuple dq = empty_tuple();
    tuple pq = empty_tuple();
    tuple wq = empty_tuple();

    if ~ d_queue.is_null() {
        dq~tpush(d_queue);
    }
    var i = 0;
    repeat queue.at(0).tlen() {
        dq~tpush(queue.at(0).at(i));
        i = i + 1;
    }

    if ~ p_queue.is_null() {
        pq~tpush(p_queue);
    }
    var i = 0;
    repeat queue.at(1).tlen() {
        pq~tpush(queue.at(1).at(i));
        i = i + 1;
    }

    if ~ w_queue.is_null() {
        wq~tpush(w_queue);
    }
    var i = 0;
    repeat queue.at(2).tlen() {
        wq~tpush(queue.at(2).at(i));
        i = i + 1;
    }

    queue = ut([dq, pq, wq]);

    return (queue, end);
}

() forward(int end_i, int end_j, tuple queue) impure inline {
    int end = 0;
            repeat 1000 {
                if tlen(queue.at(0)) > 0 {
                    var row = queue.at(0);
                    var batch = row~tpop();
                    queue~tset(0, row);
                    (queue, end) = step_forward(queue, batch);
                    if (end == -1) {
                        return ();
                    }
                } else {
                    if tlen(queue.at(1)) > 0 {
                        var row = queue.at(1);
                        var batch = row~tpop();
                        queue~tset(1, row);
                        (queue, end) = step_forward(queue, batch);
                        if (end == -1) {
                            return ();
                        }
                    } else {
                        if tlen(queue.at(2)) > 0 {
                            var row = queue.at(2);
                            var batch = row~tpop();
                            queue~tset(2, row);
                            (queue, end) = step_forward(queue, batch);
                            if (end == -1) {
                                return ();
                            }
                        } else {
                            return ();
                        }
                    }
                }
            }
    return ();
}

(int, int, int) back(int i, int j) inline {

    var best_w = max_w;
    var best_i = i;
    var best_j = j;

    (best_i, best_j, best_w) = get_path(best_i, best_j, i + 1, j + 1, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i + 1, j - 1, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i - 1, j + 1, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i - 1, j - 1, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i - 1, j, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i, j + 1, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i + 1, j, best_w);
    (best_i, best_j, best_w) = get_path(best_i, best_j, i, j - 1, best_w);
    return(best_i, best_j, best_w);
}

(int, int, int, tuple) solve(int n, int m, tuple maze) method_id {

    set_gas_limit(100000000);

    g_n = n;
    g_m = m;
    g_maze = maze;
    tuple lmap_init = empty_tuple();

    ;; INIT
    var start_i = 0;
    var start_j = 0;
    var end_i = 0;
    var end_j = 0;

    ;; FILL LMAP, START, END POINTS
    var i = 0;
    repeat (n) {
        var j = 0;
        var row = empty_tuple();
        repeat (m) {
            var item = maze.at(i).at(j);
            if maze.at(i).at(j) == s {
                start_i = i;
                start_j = j;
                row~tpush(0);
            } else {
                row~tpush(-1);
            }
            if maze.at(i).at(j) == e {
                end_i = i;
                end_j = j;
            }
            j = j + 1;
        }
        lmap_init~tpush(row);
        i = i + 1;
    }

    if (abs(start_i - end_i) < 2) & (abs(start_j - end_j) < 2) { ;; check for zero path
        return (0, 0, 1, maze);
    }

    lmap = lmap_init;

    tuple init_batch = null();
    init_batch~push_back(ut([start_i, start_j, 0]));

    tuple init_queue = ut([
            ut([init_batch]),              ;; dot queue
            empty_tuple(),                 ;; prob queue
            empty_tuple()                  ;; wall queue
    ]);

    ;; Forward trace
    forward(end_i, end_j, init_queue);

    ;; Back trace
    var cur_i = end_i;
    var cur_j = end_j;
    lmap = lmap.write_at(end_i, end_j, max_w);

    (cur_i, cur_j, int w) = back(end_i, end_j);

    var walls = w / wall_w;
    var prob = (w - walls * wall_w) / prob_w;
    var path = w - walls * wall_w - prob * prob_w + 1;

    var solved_maze = maze;
    solved_maze = write_at(solved_maze, cur_i, cur_j, p);

    repeat (1000) {
        (cur_i, cur_j, int cur_w) = back(cur_i, cur_j);
        if cur_w == 0 {
            return (walls, prob, path, solved_maze);
        }
        solved_maze = write_at(solved_maze, cur_i, cur_j, p);
    };

    return (-1, 0, 0, null());
}
